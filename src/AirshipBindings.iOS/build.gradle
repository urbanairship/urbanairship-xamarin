ext {
    pod = 'UrbanAirship-iOS-SDK'
    tempBindingsFolder = new File("$buildDir/bindings")
}

task generateBindings << {
    if (!tempBindingsFolder.exists()) {
        tempBindingsFolder.mkdirs()
    }

    // Get the pod
    exec {
        workingDir "$tempBindingsFolder"
        commandLine "sharpie", "pod", "init", "ios", "$pod"
    }

    // TODO: As of 03/23/2017, Xamarin cannot pull down specific pod
    // versions. Have to replace the version and run pod install.
    ant.replaceregexp(
        file: "$tempBindingsFolder/Podfile",
        match: "pod '$pod'.*",
        replace: "pod '$pod', '$airshipProperties.iosVersion'"
    )

    exec {
        workingDir "$tempBindingsFolder"
        commandLine "pod", "install"
    }

    // Bind the pod
    exec {
        workingDir "$tempBindingsFolder"
        commandLine "sharpie", "pod", "bind"
    }
}

task syncVersion << {
    ant.replaceregexp(
        file: "$rootDir/Cartfile",
        match: "\"urbanairship/ios-library\" == .*",
        replace: "\"urbanairship/ios-library\" == $airshipProperties.iosVersion"
    )
}

task clean(type: Delete) {
    delete "bin"
    delete "obj"
    delete "$buildDir"
}

task build << {
    exec {
        workingDir "$rootDir"
        commandLine "carthage", "update"
    }

    exec {
        commandLine "strip", "-S", "-x", "../../Carthage/Build/iOS/AirshipKit.framework/AirshipKit"
    }

    exec {
        commandLine "mono", "$nugetExe", "restore", "AirshipBindings.iOS.sln"
    }

    exec {
        commandLine "$mdTool", "build", "-c:Release", "AirshipBindings.iOS.sln"
    }
}
build.dependsOn('syncVersion')
