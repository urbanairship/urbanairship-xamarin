ext {
    pod = 'UrbanAirship-iOS-SDK'
    tempBindingsFolder = new File("$buildDir/bindings")
}

task clean(type: Delete) {
    delete "bin"
    delete "obj"
    delete "$buildDir"
}

task syncVersion  {
    doLast() {
        ant.replaceregexp(
            file: "$rootDir/Cartfile",
            match: "\"urbanairship/ios-library\" == .*",
            replace: "\"urbanairship/ios-library\" == $airshipProperties.iosVersion"
        )
    }
}

task carthageUpdate {
    doLast() {
        exec {
            workingDir "$rootDir"
            commandLine "carthage", "update"
        }

        exec {
            commandLine "strip", "-S", "-x", "../../Carthage/Build/iOS/AirshipKit.framework/AirshipKit"
        }
    }
}

task build {
    doLast() {
        exec {
            commandLine "mono", "$nugetExe", "restore", "AirshipBindings.iOS.sln"
        }

        exec {
            commandLine "${msbuild}", "/t:build", "/p:Configuration=Release", "AirshipBindings.iOS.sln"
        }
    }
}

build.dependsOn('carthageUpdate')
carthageUpdate.dependsOn('syncVersion')

task generateBindings {
    doLast() {
        if (!tempBindingsFolder.exists()) {
            tempBindingsFolder.mkdirs()
        }

        exec {
            workingDir "$tempBindingsFolder"
            commandLine "sharpie", "bind", "-framework", "$rootDir/Carthage/Build/iOS/AirshipKit.framework"
        }
    }
}
generateBindings.dependsOn('carthageUpdate')
