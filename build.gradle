ext {
    airshipProperties = new Properties()
    airshipProperties.load(new FileInputStream("airship.properties"))
    toolsDir = new File("$projectDir/.tools")
    if ( !toolsDir.exists() ) {
        toolsDir.mkdirs()
    }
    rootDir = new File("$projectDir")
    nugetExe = new File("$toolsDir/nuget.exe")
    if (!nugetExe.exists()) {
        new URL('https://dist.nuget.org/win-x86-commandline/latest/nuget.exe').withInputStream{
            i -> nugetExe.withOutputStream{ it << i }
        }
    }
    msbuild = "/Library/Frameworks/Mono.framework/Commands/msbuild"
}

// Build nuget pkgs -- can pass in parameters (nuspec files) to specify which
// nupkg files to build
task pkg {
    doLast() {
        def nuspecs = []
        if (project.hasProperty('nuspec')) {
            nuspecs = nuspec.split(',')
        } else {
            nuspecs = [
                "UrbanAirship.Android.Core.nuspec",
                "UrbanAirship.Android.ADM.nuspec",
                "UrbanAirship.Android.FCM.nuspec",
                "UrbanAirship.Android.GCM.nuspec",
                "UrbanAirship.Portable.nuspec",
                "UrbanAirship.NETStandard.nuspec",
                "UrbanAirship.iOS.nuspec",
                "UrbanAirship.iOS.AppExtensions.nuspec"
            ]
        }

        for (nuspec in nuspecs) {
            exec {
                commandLine "mono", "${nugetExe}", "pack", "$nuspec", "-OutputDirectory", "$buildDir"
            }
        }
    }
}
pkg.dependsOn('build')

// Create local nuget feed
task createLocalFeed {
    doLast() {
        // add doesn't have a "-force" option, so first remove all the nuget packages from the local feed
        File localNugetFeed = file("${rootDir}/local-nuget-feed")
        localNugetFeed.deleteDir()

        // get all the nuget packages from the build directory
        def nupkgs = buildDir.listFiles(new FilenameFilter() {
            public boolean accept(File dir, String name) {
                return name.toLowerCase().endsWith(".nupkg");
            }
        })

        // add the packages to the local nuget feed
        nupkgs.each { nupkg ->
            // add the package to the local nuget feed
            exec {
                commandLine "mono", "${nugetExe}", "add", "$nupkg", "-source", "$localNugetFeed"
            }
        }

        // clear the local nuget cache (so new packages with unchanged versions are used)
        exec {
            commandLine "mono", "${nugetExe}", "locals", "all", "-clear"
        }

    }
}
createLocalFeed.dependsOn('pkg')

// Sync the version with the version declared in the airship.properties file.
task syncVersion {
    doLast() {

        def androidNugetVersion = airshipProperties.androidVersion
        if (airshipProperties['androidPackageVersion']) {
            androidNugetVersion = "${airshipProperties.androidVersion}.${airshipProperties.androidPackageVersion}"
        }

        def iosNugetVersion = airshipProperties.iosVersion
        if (airshipProperties['iosPackageVersion']) {
            iosNugetVersion = "${airshipProperties.iosVersion}.${airshipProperties.iosPackageVersion}"
        }

        // Handle Android nuspec versions
        ant.replaceregexp(
            match: "<version>.*</version>",
            replace: "<version>$androidNugetVersion</version>"
        ) {
            fileset(dir: "$projectDir", includes: "UrbanAirship.Android*.nuspec")
        }

        // Handle iOS nuspec versions
        ant.replaceregexp(
            match: "<version>.*</version>",
            replace: "<version>$iosNugetVersion</version>"
        ) {
            fileset(dir: "$projectDir", includes: "UrbanAirship.iOS*.nuspec")
        }

        // Handle Portable & .NET Standard versions
        ant.replaceregexp(
            file: "$projectDir/UrbanAirship.Portable.nuspec",
            match: "<version>.*</version>",
            replace: "<version>$airshipProperties.libVersion</version>",
            flags: "g"
        )
        ant.replaceregexp(
            file: "$projectDir/UrbanAirship.NETStandard.nuspec",
            match: "<version>.*</version>",
            replace: "<version>$airshipProperties.libVersion</version>",
            flags: "g"
        )

        // handle urbanairship.android.core dependencies
        ant.replaceregexp(
            match: "<dependency id=\"urbanairship.android.core\" version=\".*\"/>",
            replace: "<dependency id=\"urbanairship.android.core\" version=\"$androidNugetVersion\"/>",
            flags: "g"
        ) {
            fileset(dir: "$projectDir", includes: "UrbanAirship*.nuspec")
        }

        // handle urbanairship.ios dependencies
        ant.replaceregexp(
            match: "<dependency id=\"urbanairship.ios\" version=\".*\"/>",
            replace: "<dependency id=\"urbanairship.ios\" version=\"$iosNugetVersion\"/>",
            flags: "g"
        ) {
            fileset(dir: "$projectDir", includes: "UrbanAirship*.nuspec")
        }

        // Shared assembly info
        ant.replaceregexp(
            file: "$projectDir/src/SharedAssemblyInfo.cs",
            match: "AssemblyVersion (.*)]",
            replace: "AssemblyVersion (\"$airshipProperties.libVersion\")]"
        )
    }
}

class PublishTask extends DefaultTask {
    @InputDirectory
    @Optional
    def File nupkgDir = getProject().buildDir

    @Input
    @Optional
    def File nugetExec = getProject().nugetExe

    @Input
    def String nugetRepoURL

    @Input
    def String nugetApiKey

    @TaskAction
    def publish() {
        // get all the nuget packages from the build directory
        def nupkgs = nupkgDir.listFiles(new FilenameFilter() {
            public boolean accept(File dir, String name) {
                return name.toLowerCase().endsWith(".nupkg");
            }
        })

        // publish the packages to nuget
        nupkgs.each { nupkg ->
            // publish the package
            getProject().exec {
                ignoreExitValue true
                commandLine "mono", "${nugetExec}", "push", "$nupkg", "-source", "${nugetRepoURL}", "-apikey", "${nugetApiKey}"
            }
        }
    }
}

task publishToStaging(type:PublishTask) {
    nugetRepoURL = "https://int.nugettest.org/api/v2/package"
    nugetApiKey = "$System.env.NUGET_STAGING_API_KEY"
}
publishToStaging.dependsOn('pkg')

task publishToProduction(type:PublishTask) {
    nugetRepoURL = "https://www.nuget.org/api/v2/package"
    nugetApiKey = "$System.env.NUGET_PRODUCTION_API_KEY"
}
publishToProduction.dependsOn('pkg')

//--- GLOBAL TASKS ---//

// Clean all
task clean(type: Delete) {
    doLast() {
        project.delete "$buildDir"
    }
}

// Build the plugin
task build
build.mustRunAfter('clean')
build.dependsOn('syncVersion')
build.dependsOn(':docs:build',
    ':src:AirshipBindings.Android.ADM:build',
    ':src:AirshipBindings.Android.Core:build',
    ':src:AirshipBindings.Android.FCM:build',
    ':src:AirshipBindings.Android.GCM:build',
    ':src:AirshipBindings.NETStandard:build',
    ':src:AirshipBindings.Portable:build',
    ':src:AirshipBindings.iOS:build',
    ':src:AirshipBindings.iOS.AppExtensions:build')
